# --- Dc1394 ---
if(NOT HAVE_DC1394_2 AND PKG_CONFIG_FOUND)
  pkg_check_modules(DC1394_2 libdc1394-2 QUIET)
  if(DC1394_2_FOUND)
    set(DC1394_2_VERSION "${DC1394_2_VERSION}" PARENT_SCOPE) # informational
    set(HAVE_DC1394_2 TRUE)
  endif()
endif()

if(NOT HAVE_DC1394_2)
  find_path(DC1394_INCLUDE "dc1394/dc1394.h"
    PATHS "${DC1394_ROOT}" ENV DC1394_ROOT
    PATH_SUFFIXES "include"
    NO_DEFAULT_PATH)
  find_library(DC1394_LIBRARY "dc1394"
    PATHS "${DC1394_ROOT}" ENV DC1394_ROOT
    PATH_SUFFIXES "lib"
    NO_DEFAULT_PATH)
  if(DC1394_INCLUDE AND DC1394_LIBRARY)
    set(HAVE_DC1394_2 TRUE)
    set(DC1394_2_INCLUDE_DIRS "${DC1394_INCLUDE}")
    set(DC1394_2_LIBRARIES "${DC1394_LIBRARY}")
    set(DC1394_2_VERSION "unknown" PARENT_SCOPE) # informational
  endif()
endif()

if(HAVE_DC1394_2)
  add_library(ocv::3rdparty::dc1394_2 INTERFACE IMPORTED)
  set_target_properties(ocv::3rdparty::dc1394_2 PROPERTIES
    INTERFACE_INCLUDE_DIRECTORIES "${DC1394_2_INCLUDE_DIRS}"
    INTERFACE_LINK_LIBRARIES "${DC1394_2_LIBRARIES}"
    INTERFACE_COMPILE_DEFINITIONS "HAVE_DC1394_2")
endif()

set(HAVE_DC1394_2 ${HAVE_DC1394_2} PARENT_SCOPE)
